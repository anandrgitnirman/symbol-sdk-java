plugins {
    id "org.openapi.generator" version "4.1.0"
}

task clean(type: Delete) {
    delete "$rootDir/target"
}

ext {
    generateVersion = "0.7.17-SNAPSHOT"
}

//NOTE - For some reason the library field is ignored. I had to create a configFile per library.

["vertx", "okhttp-gson", "jersey2"].eachWithIndex { library, index ->
    task "generate_${library}"(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
        version = "${generateVersion}".toString()
        id = "nem2-sdk-java-openapi-${library}".toString()
        configFile = "$rootDir/${library}.json".toString()
        library = "${library}".toString()
        groupId = 'io.nem'.toString()
        generatorName = "java"
        inputSpec = "$rootDir/openapi3-any-of-patch.yaml".toString()
        apiPackage = "io.nem.sdk.openapi.${library}.api".toString()
        invokerPackage = "io.nem.sdk.openapi.${library}.invoker".toString()
        modelPackage = "io.nem.sdk.openapi.${library}.model".toString()
        outputDir = "$rootDir/target/nem2-sdk-java-openapi-${library}".toString()
    }
}

task generate

generate.dependsOn "generate_vertx"
generate.dependsOn "generate_okhttp-gson"
generate.dependsOn "generate_jersey2"


subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    javadoc {
        failOnError = false
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    build.dependsOn generate

    if (project.hasProperty("signing.keyId")) {
        apply plugin: 'signing'
        signing {
            sign configurations.archives
        }
    }
    uploadArchives {

        def username = project.hasProperty("ossrhUsername") ? project.ossrhUsername : ""

        def password = project.hasProperty("ossrhPassword") ? project.ossrhPassword : ""

        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: username, password: password)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: username, password: password)
                }

                pom.project {
                    packaging 'jar'
                    url 'https://github.com/nemtech/nem2-sdk-java'
                    scm {
                        connection 'scm:git:https://github.com/nemtech/nem2-sdk-java.git'
                        developerConnection 'scm:svn:https://github.com/nemtech/nem2-sdk-java'
                        url 'https://github.com/nemtech/nem2-sdk-java'
                    }
                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                }
            }
        }
    }
}
